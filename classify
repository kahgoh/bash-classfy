#!/usr/bin/zsh

# Names of months for which the financial year is the following year.
NEXT_YEAR_MONTHS=("JUL" "AUG" "SEP" "OCT" "NOV" "DEC")

# All the months in a year.
MONTHS=("JAN" "FEB" "MAR" "APR" "MAY" "JUN" $NEXT_YEAR_MONTHS)

filesDirectory='.'
if ((${#} > 0)); then
  filesDirectory=${1}
fi

for file in ${filesDirectory}/*.pdf; do
  # The month and year are at the end of the file name, in the form MMMyyyy.
  name=${file:t:r}
  length=${#name}
  year=${name[${length}-3, ${length}]}
  month=${name[${length}-6, ${length}-4]}

  # Validate the year and month. Year is always a a 4 digit number and month
  # must be in MONTHS array.
  if [[ ${year} =~ '^[0-9]{4}$' && ${MONTHS[(I)${month}]} -gt 0 ]]; then

    if [[ ${NEXT_YEAR_MONTHS[(I)${month}]} -gt 0 ]]; then
      year=$((${year}+1))
    fi

    destination=${filesDirectory}/${year}FY
    if [[ ! ( -d $destination) ]]; then
      mkdir ${destination}
    fi
    git mv ${file} ${destination}
  else
    echo Skipping file ${file}
  fi
done
